
string(SUBSTRING "$ENV{TRAVIS_COMMIT}" 0 7 COMMIT_ID)
set(CTEST_SITE "travis")
set(CTEST_BUILD_NAME "$ENV{TRAVIS_JOB_NUMBER}-${COMMIT_ID}-$ENV{TRAVIS_OS_NAME}-$ENV{CXX}")

set(CTEST_SOURCE_DIRECTORY "$ENV{TRAVIS_BUILD_DIR}")
set(CTEST_BINARY_DIRECTORY "${CTEST_SOURCE_DIRECTORY}/build")

configure_file(${CTEST_SOURCE_DIRECTORY}/.travis.cfg.in ${CTEST_BINARY_DIRECTORY}/travis.cfg)
set(CTEST_NOTES_FILES ${CTEST_BINARY_DIRECTORY}/travis.cfg)

set(CTEST_CMAKE_GENERATOR "Unix Makefiles")
set(CTEST_BUILD_CONFIGURATION "$ENV{CMAKE_BUILD_TYPE}")
set(CTEST_CONFIGURATION_TYPE "Debug")

find_program(CLANG_CHECK clang-check)

find_program(VALGRIND valgrind)
set(CTEST_MEMORYCHECK_COMMAND ${VALGRIND})
set(CTEST_MEMORYCHECK_COMMAND_OPTIONS "-q --track-origins=yes --leak-check=yes --show-reachable=yes --num-callers=50")
set(CTEST_MEMORYCHECK_SUPPRESSIONS_FILE ${CTEST_SOURCE_DIRECTORY}/.valgrind.supp)

find_program(GCOV gcov-4.8)
set(CTEST_COVERAGE_COMMAND ${GCOV})
  set(COVERAGE_EXTRA_FLAGS "-l -p")

ctest_start(Continuous)
ctest_configure(OPTIONS -DBUILD_FS_BACKEND=$ENV{FS_BACKEND})
ctest_build()

if(CLANG_CHECK AND $ENV{CXX} MATCHES "clang")
  ctest_build(TARGET check)
endif()

ctest_test(RETURN_VALUE tests_ret)

if(VALGRIND AND $ENV{CXX} MATCHES "clang")
  ctest_memcheck()
endif()

if(GCOV AND $ENV{CXX} MATCHES "g\\+\\+")
  ctest_coverage()
endif()

ctest_submit()

if(NOT tests_ret EQUAL 0)
  message(FATAL_ERROR "Test had failures!")
endif()
